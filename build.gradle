plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'com.google.cloud.tools.jib' version '3.4.0'
}

group = 'ie.mu.fyp'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

jib {
	from {
		image = 'openjdk:17-jdk-slim'
	}
	to {
		image = 'erikasmu/weather-service'
		tags = ['latest']
	}
	container {
		ports = ['8080']
		mainClass = 'ie.mu.fyp.weatherservice.WeatherServiceApplication'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-json'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('copyReactBuild', Copy) {
	// Copy all contents of the build directory into the static resources directory
	from 'frontend/build'
	into 'src/main/resources/static'

	// Include all files
	include '**/*'
}

tasks.register('prepareFrontend') {
	doLast {
		// Define the path to your frontend directory
		def frontendDir = "${projectDir}/frontend"
		def resourcesDir = "${projectDir}/src/main/resources/static"

		// Check if the node_modules folder exists
		def nodeModulesDir = new File(frontendDir, "node_modules")
		if (!nodeModulesDir.exists()) {
			// If node_modules doesn't exist, run npm install
			println "Installing npm dependencies..."
			exec {
				workingDir frontendDir
				commandLine 'npm', 'install'
			}
		} else {
			println "npm dependencies are already installed."
		}

		// Build the frontend project
		println "Building the frontend..."
		exec {
			workingDir frontendDir
			commandLine 'npm', 'run', 'build'
		}

		// Copy the build output to the Spring Boot resources directory
		println "Copying build resources directory..."
		copy {
			from "${frontendDir}/build"
			into resourcesDir
		}
	}
}
