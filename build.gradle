plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'com.google.cloud.tools.jib' version '3.4.0'
}

group = 'ie.mu.fyp'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

jib {
	from {
		image = 'openjdk:17-jdk-slim'
	}
	to {
		image = 'erikasmu/weather-service'
		tags = ['latest']
	}
	container {
		ports = ['8080']
		mainClass = 'ie.mu.fyp.weatherservice.WeatherServiceApplication'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-json'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('copyReactBuild', Copy) {
	// Base destination directory for the task
	into 'src/main/resources/static'

	// Copy files from 'frontend/build' excluding the 'static' subdirectory
	from('frontend/build') {
		exclude 'static/**'
		include 'favicon.ico', 'index.html', 'logo192.png', 'logo512.png', 'manifest.json', 'robots.txt', 'asset-manifest.json'
	}

	// Copy files from 'css' and 'js' directories within 'static'
	from('frontend/build/static/css') {
		into 'css'
	}
	from('frontend/build/static/js') {
		into 'js'
	}
}

processResources.dependsOn(copyReactBuild)
